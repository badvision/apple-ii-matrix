 1 REM THE MATRIX
 2 REM BY NEIL KANDALGAONKAR <NEILK@NEILK.NET>
 3 REM https://github.com/neilk/apple-ii-matrix/
 5 FOR I = 1 TO 30: PRINT " ": NEXT I
 10 DIM K(3,2)
 11 H = 0
 12 V = 1
 13 F = 2
 15 FOR C = 0 TO 3
 16 GOSUB 50
 17 NEXT C
 20 FOR C = 0 TO 3
 30 R =  INT ( RND (1) * 20)
 31 IF R = 1 THEN  GOSUB 50
 32 IF R >< 1 THEN  GOSUB 100
 35 NEXT C
 39 GOTO 20
 50 REM  RESET
 51 K(C,H) =  INT ( RND (1) * 40) + 1
 52 K(C,V) = 0
 53 K(C,F) =  INT ( RND (1) * 2)
 99  RETURN
 100 REM  DRAW
 101 K(C,V) = K(C,V) + 1
 102 IF K(C,V) > 22 THEN  RETURN
 103 VTAB K(C,V): HTAB K(C,H)
 110 IF K(C,F) = 1 THEN  GOSUB 120
 111 IF K(C,F) = 0 THEN  GOSUB 130
 119 RETURN
 120 REM  DRAW CHAR
 121 PRINT  CHR$ ( INT ( RND (1) * 96 + 32))
 122 RETURN
 130 REM  ERASE CHAR
 131 PRINT " "
 132 RETURN
